//@version=5
strategy("ğŸ‘œPercentile_SuperTrend_OBV&DI&EMA", overlay=true,
     initial_capital = 75, currency = 'USDT', default_qty_type = strategy.percent_of_equity, default_qty_value = 49, commission_value = 0.05,
     backtest_fill_limits_assumption = 0, slippage = 0, margin_long = 0, margin_short = 0, pyramiding = 0, calc_on_order_fills = false, calc_on_every_tick = false, use_bar_magnifier = true, process_orders_on_close = true)

// ============================================
// â° STRATEGY TIME RANGE
// ============================================
start_time = input.time(timestamp("2025-01-01 00:00 +0900"), 'Start', inline='ti', group='Time range/POSITION SELECTIONS')
inDate = time >= start_time

// ============================================
// ğŸ“ POSITION SELECTIONS
// ============================================
active_long = input.bool(true, 'ActLong', inline='ti', group='Time range/POSITION SELECTIONS')
active_short = input.bool(true, 'ActShort', inline='ti', group='Time range/POSITION SELECTIONS')

// ============================================
// ğŸ“ SHOW PLOT LINES 
// ============================================
show_st_line = input.bool(true, 'SuperTrend', inline='sh', group='SHOW PLOT LINES')
show_Volatility_HL_line = input.bool(true, 'HL Line', inline='sh', group='SHOW PLOT LINES')
show_ema_line = input.bool(false, 'EMA', inline='sh', group='SHOW PLOT LINES')
show_position_line = input.bool(true, 'Position', inline='sh', group='SHOW PLOT LINES')
show_tp_line = input.bool(true, 'TP', inline='sh', group='SHOW PLOT LINES')
show_sl_line = input.bool(false, 'SL', inline='sh', group='SHOW PLOT LINES')
show_vwap_line = input.bool(false, 'VWAP', inline='sh', group='SHOW PLOT LINES')

// ============================================
// ğŸ’° POSITION ENTRY FILTER ON/OFF
// ============================================
ST_on = input.bool(true, 'ST', inline='SW', group='SW: ST/ADX/OBV/MACD/DIë°©í–¥/DIì´ê²©/EMA/Volatility/VWMA')
ADX_on = input.bool(true, 'ADX', inline='SW', group='SW: ST/ADX/OBV/MACD/DIë°©í–¥/DIì´ê²©/EMA/Volatility/VWMA')
OBV_on = input.bool(true, 'OBV', inline='SW', group='SW: ST/ADX/OBV/MACD/DIë°©í–¥/DIì´ê²©/EMA/Volatility/VWMA')
MACD_on = input.bool(true, 'MACD', inline='SW', group='SW: ST/ADX/OBV/MACD/DIë°©í–¥/DIì´ê²©/EMA/Volatility/VWMA')
DI_on = input.bool(true, 'DI ë°©í–¥ì„±', inline='SW', group='SW: ST/ADX/OBV/MACD/DIë°©í–¥/DIì´ê²©/EMA/Volatility/VWMA')
Limit_on = input.bool(true, 'DI ì´ê²©ë„', inline='SW', group='SW: ST/ADX/OBV/MACD/DIë°©í–¥/DIì´ê²©/EMA/Volatility/VWMA')
EMA_on = input.bool(true, 'EMA', inline='SW', group='SW: ST/ADX/OBV/MACD/DIë°©í–¥/DIì´ê²©/EMA/Volatility/VWMA')
Volatility_on = input.bool(true, 'Volatility', inline='SW', group='SW: ST/ADX/OBV/MACD/DIë°©í–¥/DIì´ê²©/EMA/Volatility/VWMA')
VWAP_on = input.bool(false, 'VWAP', inline='SW', group='SW: ST/ADX/OBV/MACD/DIë°©í–¥/DIì´ê²©/EMA/Volatility/VWMA')

// ============================================
// ğŸ’° POSITION EXIT FILTER ON/OFF
// ============================================
exit_on_opposite = input.bool(true, 'ì²´í¬ëœ ì‹ í˜¸ë°˜ì „ ì‹œ ì²­ì‚° â‡›', inline='exit2', group='Switching Position_EXIT Filters')
exit_on_st_flip = input.bool(false, 'SuperTrend', inline='exit2', group='Switching Position_EXIT Filters')
exit_on_di_flip = input.bool(false, 'DIë°©í–¥', inline='exit2', group='Switching Position_EXIT Filters')
exit_on_macd_flip = input.bool(false, 'MACD', inline='exit2', group='Switching Position_EXIT Filters')

// VWAP (Volume Weighted Average Price
vwapValue = ta.vwap(hlc3)
VWAP_L = close > vwapValue
VWAP_S = close < vwapValue

// ============================================
// ğŸ’° ENTRY Filters : EMA TREND LINE
// ============================================
emalen_fast = input.int(8, minval=1, title="EMA Fast", inline = 'ema', group = 'ENTRY Filters : EMA TREND LINE/Volatility Limit Setting')
emalen_slow = input.int(13, minval=1, title="EMA Slow", inline = 'ema', group = 'ENTRY Filters : EMA TREND LINE/Volatility Limit Setting')
src_ema = input(close, title="", inline = 'ema', group = 'ENTRY Filters : EMA TREND LINE/Volatility Limit Setting')
ema_fast = ta.ema(src_ema, emalen_fast)
ema_slow = ta.ema(src_ema, emalen_slow)
EMA_L = ema_fast > ema_slow and ema_fast > ema_fast[1] 
EMA_S = ema_fast < ema_slow and ema_slow[1] > ema_slow 

// ============================================
// ğŸ’° ENTRY Filters : Volatility Limit Setting
// ============================================
int rangeLookback = input.int(35, title=" Volatility Length", inline = 'vltt', group = 'ENTRY Filters : EMA TREND LINE/Volatility Limit Setting')
float minRangePercent = input.float(3.3, title=" Volatility Limit(%)", minval=0.1, step=0.1, inline = 'vltt', group = 'ENTRY Filters : EMA TREND LINE/Volatility Limit Setting')/100
float highestHigh_v = ta.highest(high, rangeLookback)
float lowestLow_v = ta.lowest(low, rangeLookback)
float rangePercent = (highestHigh_v-lowestLow_v)/close
bool Volatility = rangePercent > minRangePercent

// ============================================
// ğŸŸ¢ DI( Disparity Index)
// ============================================
use_di_filter = input.bool(true, '+DI/-DIë°©í–¥|', inline='di', group='ENTRY Filters : DI /ADX / OBV / MACD', tooltip='ADXë¿ë§Œ ì•„ë‹ˆë¼ +DI/-DI ë°©í–¥ì„±ë„ í™•ì¸')
length_di = input.int(33, title = 'DI ê¸¸ì´', minval = 1, inline = 'di', group = 'ENTRY Filters : DI /ADX / OBV / MACD')
src_di = input.source(close, title = 'DI Src', inline = 'di', group = 'ENTRY Filters : DI /ADX / OBV / MACD')
sma_di = ta.sma(src_di, length_di)
di = sma_di > 0 ? 100 * (src_di - sma_di) / sma_di : 0
DI_L = di >= 0
DI_S = di < 0

// ============================================
// ğŸ“Š ADX / ADX TREND FILTERS
// ============================================
dmiLength = input.int(15, title="DMI DI", minval=1, inline='adx', group='ENTRY Filters : DI /ADX / OBV / MACD')
adxSmoothing = input.int(22, title="ADXí‰í™œ", minval=1, inline='adx', group='ENTRY Filters : DI /ADX / OBV / MACD')
adxThreshold = input.float(22.0, title="ADXì„ê³„", minval=10, step=0.1, inline='adx', group='ENTRY Filters : DI /ADX / OBV / MACD')
[plusDI, minusDI, adx] = ta.dmi(dmiLength, adxSmoothing)
ADX_L = use_di_filter ? (adx > adxThreshold and plusDI > minusDI) : adx > adxThreshold
ADX_S = use_di_filter ? (adx > adxThreshold and minusDI > plusDI) : adx > adxThreshold

// ============================================
// ğŸŸ¢ OBV(On Balance Volume Oscillator)
// ============================================
length_obv = input.int(12, title="OBV EMA", inline='obv', group='ENTRY Filters : DI /ADX / OBV / MACD')
Baseline_long = input.int(0, title="ê¸°ì¤€ì„ (ë¡±)", minval=0, step=1, inline='obv', group='ENTRY Filters : DI /ADX / OBV / MACD') * 10000
Baseline_short = input.int(2, title="(ìˆ)", minval=0, step=1, inline='obv', group='ENTRY Filters : DI /ADX / OBV / MACD') * -10000
float obv_val = ta.obv
float obv_osc = obv_val - ta.ema(obv_val, length_obv)
bool OBV_L = obv_osc > Baseline_long
bool OBV_S = obv_osc < Baseline_short

// ============================================
// ğŸ“ˆ MACD DEMA SETTINGS
// ============================================
fastLen_macd = input.int(21, title='MACD Fast length', inline='macd', group='ENTRY Filters : DI /ADX / OBV / MACD')
slowLen_macd = input.int(93, title='MACD Slow length', inline='macd', group='ENTRY Filters : DI /ADX / OBV / MACD')
dema(source, length) =>
    ema1 = ta.ema(source, length)
    ema2 = ta.ema(ema1, length)
    2 * ema1 - ema2

macdDemaLine = dema(close, fastLen_macd) - dema(close, slowLen_macd)
MACD_L = macdDemaLine > macdDemaLine[1]
MACD_S = macdDemaLine < macdDemaLine[1]

// ============================================
// ğŸ¯ ì§„ì… ì´ê²©ë„ ì œí•œ / Advanced Disparity(ì´ê²©ë„) Index
// ============================================
emaPeriod = input.int(15, title="EMA Period", inline = 'di1', group='Entry Limit Filter : by Advanced Disparity State', tooltip='Disparity(ì´ê²©ë„) ëŒ€ë¹„ ê°€ê²©ì´ ë„ˆë¬´ ë†’ìœ¼ë©´ ì§„ì… ì°¨ë‹¨')
lookbackPeriod_ma = input.int(14, title="Lookback Period", inline = 'di1', group='Entry Limit Filter : by Advanced Disparity State')
overBought_ma = input.float(93, title="Overbought Level", inline = 'di2', group='Entry Limit Filter : by Advanced Disparity State')
overSold_ma = input.float(10, title="Oversold Level", inline = 'di2', group='Entry Limit Filter : by Advanced Disparity State', tooltip='70~100 ì´ìƒ ë¡± í¬ì§€ì…˜ ì§„ì… ì°¨ë‹¨ / 30~0 ì´í•˜ ìˆ í¬ì§€ì…˜ ì§„ì… ì°¨ë‹¨')
smoothingPeriod_ma = input.int(9, title="Smoothing Period", inline = 'di3', group='Entry Limit Filter : by Advanced Disparity State')
smoothingType_ma = input.string("EMA", title="Smoothing Type", options=["EMA", "SMA"], inline = 'di3', group='Entry Limit Filter : by Advanced Disparity State')
price = close
emaValue = ta.ema(price, emaPeriod)
disparityIndex = (price - emaValue) / emaValue * 100
highestHigh = ta.highest(disparityIndex, lookbackPeriod_ma)
lowestLow = ta.lowest(disparityIndex, lookbackPeriod_ma)
range_ = highestHigh - lowestLow
scaledDisparityIndex = nz(range_) == 0 ? 0 : (disparityIndex - lowestLow) / range_ * 100
smoothedDisparityIndex = switch smoothingType_ma
    "EMA" => ta.ema(scaledDisparityIndex, smoothingPeriod_ma)
    "SMA" => ta.sma(scaledDisparityIndex, smoothingPeriod_ma)
    => na
Limit_L = (smoothedDisparityIndex < overBought_ma)
Limit_S = (smoothedDisparityIndex > overSold_ma)
high_volatility = (smoothedDisparityIndex > overBought_ma) or (smoothedDisparityIndex < overSold_ma)

// ============================================
// ğŸ”§ PERCENTILE SUPERTREND  INPUT & CALCULATION
// ============================================
Percent25 = input.int(25, "25 Percent length", inline='%', group="25-75 Percentile SuperTrend/ë°±ë¶„ìœ„ìˆ˜(Percentile)")
Percent75 = input.int(75, "75 Percent length", inline='%', group="25-75 Percentile SuperTrend/ë°±ë¶„ìœ„ìˆ˜(Percentile)")
subject_per = input.int(125, "Supertrend length", minval=2, inline='sp', group="25-75 Percentile SuperTrend/ë°±ë¶„ìœ„ìˆ˜(Percentile)") //129
mult_per = input.float(1.74, "Multiplier", step=0.01, inline='sp', group="25-75 Percentile SuperTrend/ë°±ë¶„ìœ„ìˆ˜(Percentile)") //1.75
slen_per = input.int(3, "Percentile length", inline='sp', group="25-75 Percentile SuperTrend/ë°±ë¶„ìœ„ìˆ˜(Percentile)")
src_2575 = input.source(close, "Median smoothing source", inline='sp', group="25-75 Percentile SuperTrend/ë°±ë¶„ìœ„ìˆ˜(Percentile)")
// PERCENTILE SUPERTREND  INPUT & CALCULATION
Percentile_SuperTrend_func(mult, atrPeriod, src_long, src_short) =>
    atr = ta.atr(atrPeriod)
    upper = src_long + mult * atr
    lower = src_short - mult * atr
    pl = nz(lower[1], lower)
    pu = nz(upper[1], upper)
    lower := lower > pl or close[1] < pl ? lower : pl
    upper := upper < pu or close[1] > pu ? upper : pu
    
    int dist = 0
    float st = na
    pt = nz(st[1], st)
    
    if close > upper[1]
        dist := 1
    else if close < lower[1]
        dist := -1
    else
        dist := nz(dist[1], 0)

    st := dist == 1 ? lower : upper
    [st, dist]

// ğŸ¯ PERCENTILE SUPERTREND FUNCTION
smooth_lower = ta.percentile_nearest_rank(src_2575, slen_per, Percent25)
smooth_upper = ta.percentile_nearest_rank(src_2575, slen_per, Percent75)
[x, dist] = Percentile_SuperTrend_func(mult_per, subject_per, smooth_upper, smooth_lower)
ST_L = dist == 1
ST_S = dist == -1

// ============================================
// ğŸ’° TAKE PROFIT & STOP LOSS SETTINGS
// ============================================
use_tp = input.bool(true, 'TP ì‚¬ìš©', inline='tp', group='Take Profit / Stop Loss')
long_tp_percent = input.float(9.3, 'Long TP(%)', minval=0.1, step=0.1, inline='tp', group='Take Profit / Stop Loss') //9.9
short_tp_percent = input.float(10.2, 'Short TP(%)', minval=0.1, step=0.1, inline='tp', group='Take Profit / Stop Loss')
use_sl = input.bool(true, 'SL ì‚¬ìš©', inline='sl', group='Take Profit / Stop Loss')
long_sl_percent = input.float(9.3, 'Long SL(%)', minval=0.1, step=0.1, inline='sl', group='Take Profit / Stop Loss') //9.3
short_sl_percent = input.float(12.1, 'Short SL(%)', minval=0.1, step=0.1, inline='sl', group='Take Profit / Stop Loss') //13.9

// TAKE PROFIT & STOP LOSS CALCULATION
long_tp_price = strategy.position_avg_price * (1 + long_tp_percent / 100)
short_tp_price = strategy.position_avg_price * (1 - short_tp_percent / 100)
long_sl_price = strategy.position_avg_price * (1 - long_sl_percent / 100)
short_sl_price = strategy.position_avg_price * (1 + short_sl_percent / 100)

// ============================================
// ğŸš€ POSITION ENTRY Filters
// ============================================
valid_open_long =
     active_long and
     inDate and
     (not ST_on    or ST_L) and
     (not DI_on    or DI_L) and
     (not ADX_on   or ADX_L) and
     (not OBV_on   or OBV_L) and
     (not MACD_on  or MACD_L) and
     (not Limit_on or Limit_L) and
     (not EMA_on   or EMA_L) and     
     (not VWAP_on or VWAP_L) and
     (not  Volatility_on or  Volatility)

valid_open_short =
     active_short and
     inDate and
     (not ST_on    or ST_S) and
     (not DI_on    or DI_S) and
     (not ADX_on   or ADX_S) and
     (not OBV_on   or OBV_S) and
     (not MACD_on  or MACD_S) and
     (not Limit_on or Limit_S) and
     (not EMA_on   or EMA_S) and       
     (not VWAP_on or VWAP_S) and
     (not  Volatility_on or  Volatility)

// ============================================
// ğŸ“ EXIT POSITION MANAGEMENT
// ============================================
// EXIT ON OPPOSITE SIGNAL / í¬ì§€ì…˜ ë³´ìœ  ì—¬ë¶€ í™•ì¸
has_long  = strategy.position_size > 0
has_short = strategy.position_size < 0

// ë¡± í¬ì§€ì…˜ ì¡°ê¸° ì²­ì‚° ì¡°ê±´ (ë…¼ë¦¬ì‹ìœ¼ë¡œ í†µí•©)
should_exit_long = has_long and exit_on_opposite and (
     (exit_on_st_flip and ST_S) or
     (exit_on_di_flip and DI_S) or
     (exit_on_macd_flip and MACD_S)
     )

// ìˆ í¬ì§€ì…˜ ì¡°ê¸° ì²­ì‚° ì¡°ê±´ (ë…¼ë¦¬ì‹ìœ¼ë¡œ í†µí•©)
should_exit_short = has_short and exit_on_opposite and (
     (exit_on_st_flip and ST_L) or
     (exit_on_di_flip and DI_L) or
     (exit_on_macd_flip and MACD_L)
     )

// ============================================
// ğŸ“ ENTRY POSITION MANAGEMENT
// ============================================
if valid_open_long
    strategy.entry("Long", strategy.long, comment="EğŸŸ¢L")

if valid_open_short
    strategy.entry("Short", strategy.short, comment="EğŸ”µS")

// TP/SL ë° ì¡°ê¸° ì²­ì‚° ë¡œì§
if has_long
    if should_exit_long
        strategy.close(id="Long", comment="ExitğŸ”„")
    else
        strategy.exit(id="Exit Long", from_entry="Long",
         limit=use_tp ? long_tp_price : na,
         stop=use_sl ? long_sl_price : na,
         comment_profit="TğŸ‘œP", comment_loss="LâŒSL")

if has_short
    if should_exit_short
        strategy.close(id="Short", comment="ExitğŸ”„")
    else
        strategy.exit(id="Exit Short", from_entry="Short",
         limit=use_tp ? short_tp_price : na,
         stop=use_sl ? short_sl_price : na,
         comment_profit="TğŸ§°P", comment_loss="SâŒSL")


// ============================================
// ğŸ¨ VISUAL COMPONENTS
// ============================================
fill_color = inDate ? (valid_open_long ? color.new(color.rgb(45, 162, 252), 85) : valid_open_short ? color.new(#eb05f3, 85) : color.new(color.yellow, 60)) : na

// VWAP LINE
plot(show_vwap_line ? vwapValue : na, "VWAP", color=color.new(color.white, 0), linewidth = 2)

// Volatility High-Low Line
p_highestHigh=plot(inDate and show_Volatility_HL_line ? highestHigh_v : na, title="Highest Line", color=color.new(color.orange, 0), linewidth = 2)
p_lowest=plot(inDate and show_Volatility_HL_line ? lowestLow_v : na, title="lowest Line", color=color.new(color.blue, 0), linewidth = 4)
fill(p_highestHigh, p_lowest, color=fill_color)

// EMA TREND LINE
pfast=plot(inDate and show_ema_line ? ema_fast : na, title="EMA Fast Line", color=color.new(color.orange, 0), linewidth = 2)
pslow= plot(inDate and show_ema_line ? ema_slow : na, title="EMA Fast Line", color=color.new(color.blue, 0), linewidth = 4)
fill(pfast, pslow, color=fill_color)

// plotcandle í•¨ìˆ˜ë¥¼ ì‚¬ìš©í•˜ì—¬ Body, Wick, Border ìƒ‰ìƒì„ ë™ì¼í•˜ê²Œ ì§€ì • // ì°¨íŠ¸ì˜ ì›ë˜ ìº”ë“¤ì€ ì²´í¬í•´ì œí•´ì„œ ë¹„í™œì„±í™” í•  ê²ƒ
bar_color = inDate ? (DI_L ? color.new(color.lime, 15) : DI_S ? color.new(color.red, 15) : high_volatility ? color.new(color.white, 0) : na) : na
plotcandle(open, high, low, close, color = bar_color, wickcolor = bar_color, bordercolor = bar_color)

// "Percentile SuperTrend Line ê·¸ë¦¬ê¸° ë° ì±„ìš°ê¸°
st_color = inDate ? (ST_L ? color.new(color.rgb(45, 162, 252), 0) : ST_S ? color.new(color.purple, 0) : color.yellow) : na
plot_x = plot(inDate and show_st_line ? x : na, "Percentile SuperTrend", color=st_color, linewidth=4)
fill(plot_x, plot(ohlc4, 'Candle Line', color=color.new(color.gray, 99), linewidth=1), color=fill_color)

plot(show_tp_line and has_long and use_tp ? long_tp_price : na, "Long TP", color=color.new(color.green, 0), style=plot.style_linebr, linewidth=1, offset=1)
plot(show_tp_line and has_short and use_tp ? short_tp_price : na, "Short TP", color=color.new(color.orange, 0), style=plot.style_linebr, linewidth=1, offset=1)
plot(show_sl_line and has_long and use_sl ? long_sl_price : na, "Long SL", color=color.new(color.blue, 0), style=plot.style_linebr, linewidth=1, offset=1)
plot(show_sl_line and has_short and use_sl ? short_sl_price : na, "Short SL", color=color.new(color.red, 0), style=plot.style_linebr, linewidth=1, offset=1)
series = strategy.opentrades.entry_price(strategy.opentrades - 1)
plot(show_position_line ? series : na, title = 'Position', color=strategy.position_size > 0 ? color.new(#f4f801, 0) : strategy.position_size < 0 ? color.new(#fcba05, 0): na, style = plot.style_cross, linewidth = 2)



// ============================================
// ğŸ“Š TWO TABLES COMMON INPUT
// ============================================
bg_transp = input.int(85, 'ë°°ê²½ íˆ¬ëª…ë„â‡…', minval=0, maxval=100, inline='perf0', group='Performance Table')
text_transp = input.int(45, 'TEXT íˆ¬ëª…ë„â‡…', minval=0, maxval=100, inline='perf0', group='Performance Table')

// ============================================
// ğŸ“Š PERFORMANCE TABLE
// ============================================
show_performance = input.bool(true, 'ì´ìµë¥  í‘œì‹œâœ”,', inline='perf1', group='Performance Table')
perf_position = input.string('bottom_left', 'í‘œì‹œìœ„ì¹˜ğŸšƒ', options=['middle_right', 'bottom_left', 'bottom_right', 'bottom_center', 'top_left', 'top_right', 'top_center'], inline='perf1', group='Performance Table')
years_qty = input.int(1, 'í‘œì‹œë…„ìˆ˜â‡…', minval=1, inline='perf1', group='Performance Table') - 1

// --- ë³€ìˆ˜ ì„ ì–¸ ---
var color_profit = color.new(color.black, bg_transp)
var float cur_month_pnl = 0.0
var float[] month_pnl = array.new<float>()
var int[] month_time = array.new<int>()
var float[] year_pnl = array.new<float>()
var int[] year_time = array.new<int>()
var int max_array_size = 200

// --- ê³„ì‚°ë¶€ ---
float bar_pnl = strategy.equity != strategy.equity[1] and nz(strategy.equity[1]) > 0 ? (strategy.equity / strategy.equity[1] - 1) : 0.0

if show_performance
    bool new_month = month(time) != month(time[1])
    bool new_year = year(time) != year(time[1])

    cur_month_pnl := new_month ? 0.0 : (1 + nz(cur_month_pnl[1])) * (1 + bar_pnl) - 1

    if new_month
        array.push(month_pnl, cur_month_pnl[1])
        array.push(month_time, time[1])
        if array.size(month_pnl) > max_array_size
            array.shift(month_pnl)
            array.shift(month_time)

    if new_year
        float cumulative_pnl = strategy.netprofit[1] / strategy.initial_capital
        array.push(year_pnl, cumulative_pnl)
        array.push(year_time, time[1])
        if array.size(year_pnl) > max_array_size
            array.shift(year_pnl)
            array.shift(year_time)

// --- í…Œì´ë¸” ê·¸ë¦¬ê¸° ---
if show_performance and barstate.islastconfirmedhistory
    float final_cumulative_pnl = strategy.netprofit / strategy.initial_capital
    
    float[] final_year_pnl = array.copy(year_pnl)
    int[] final_year_time = array.copy(year_time)
    array.push(final_year_pnl, final_cumulative_pnl)
    array.push(final_year_time, time)
    
    float[] final_month_pnl = array.copy(month_pnl)
    int[] final_month_time = array.copy(month_time)
    array.push(final_month_pnl, cur_month_pnl)
    array.push(final_month_time, time)

    year_size = array.size(final_year_pnl)
    if year_size > 0
        year_threshold = year(time) - years_qty
        int displayed_rows = 0
        for i = 0 to year_size - 1
            if year(array.get(final_year_time, i)) >= year_threshold
                displayed_rows += 1
        
        if displayed_rows > 0
            table monthly_table = table.new(perf_position, 14, displayed_rows + 1, border_width=1)
            color header_bg_color = color.new(color.gray, bg_transp)
            color text_color = color.new(color.white, text_transp)
            
            table.cell(monthly_table, 0, 0, "ë…„ë„/ì›”ë³„", text_size=size.auto, bgcolor=header_bg_color, text_color=text_color)
            for i = 1 to 12
                table.cell(monthly_table, i, 0, str.tostring(i, "00") + "ì›”", text_size=size.auto, bgcolor=header_bg_color, text_color=text_color)
            
            // â—ï¸ ìˆ˜ì •: í…Œì´ë¸” í—¤ë”ë¥¼ 'ëˆ„ì ì´ìµ(%)'ìœ¼ë¡œ ë³€ê²½
            table.cell(monthly_table, 13, 0, "ëˆ„ì ì´ìµ(%)", text_size=size.auto, bgcolor=header_bg_color, text_color=text_color)

            var year_to_row_map = map.new<int, int>()
            int row_idx_counter = 1

            for yi = 0 to year_size - 1
                y_time = array.get(final_year_time, yi)
                y_year = year(y_time)

                if y_year >= year_threshold
                    if not map.contains(year_to_row_map, y_year)
                        map.put(year_to_row_map, y_year, row_idx_counter)
                        row_idx_counter += 1

                    int y_row = map.get(year_to_row_map, y_year)
                    table.cell(monthly_table, 0, y_row, str.tostring(y_year), text_size=size.auto, bgcolor=header_bg_color, text_color=text_color)
                    
                    year_pnl_val = array.get(final_year_pnl, yi)
                    y_color = year_pnl_val > 0 ? color.new(color.rgb(0, 73, 92), bg_transp) : color.new(color.red, bg_transp)
                    table.cell(monthly_table, 13, y_row, str.tostring(year_pnl_val * 100, "#,###"), bgcolor=y_color, text_size=size.auto, text_color=text_color) // (year_pnl_val * 100, "#,###.#'%'") ë˜ëŠ” (year_pnl_val * 100, "0.0'%'")

            month_size = array.size(final_month_pnl)
            for mi = 0 to month_size - 1
                m_time = array.get(final_month_time, mi)
                m_year = year(m_time)
                
                if m_year >= year_threshold and map.contains(year_to_row_map, m_year)
                    int m_row = map.get(year_to_row_map, m_year)
                    int m_col = month(m_time)
                    
                    month_pnl_val = array.get(final_month_pnl, mi)
                    m_color = month_pnl_val > 0 ? color_profit : color.new(color.red, bg_transp)
                    table.cell(monthly_table, m_col, m_row, str.tostring(month_pnl_val * 100, "#,###"), bgcolor=m_color, text_size=size.auto, text_color=text_color) // str.tostring(month_pnl_val * 100, "0.0'%'") ë˜ëŠ” str.tostring(month_pnl_val * 100, "#,###.#'%'")


// ============================================
// ğŸ“ˆ ENHANCED STATISTICS TABLE / âœ”â¸â–¶â˜‘ğŸ’ âœ…||ğŸšƒâ‡›â‡ğŸš¨â‡…ğŸ“ˆğŸš€
// ============================================
show_stats = input.bool(true, 'ì„±ê³¼ ê²€ì¦âœ”,', inline='perf2', group='Performance Table')
stats_position = input.string('top_right', 'ì„±ê³¼ê²€ì¦ í…Œì´ë¸” í‘œì‹œìœ„ì¹˜ğŸšƒ', options=['middle_left', 'middle_right', 'bottom_left', 'bottom_right', 'bottom_center', 'top_left', 'top_right', 'top_center'], inline='perf2', group='Performance Table')
Risk_level = input.int(50, title = '[ì´ˆë‹¹ ê±°ë˜ëŒ€ê¸ˆ] ëŒ€ë¹„ [íˆ¬ìê¸ˆì•¡]ì˜ ìœ„í—˜ ë¹„ìœ¨(%)', minval = 0, maxval=100, step=1, inline='perf3', group='Performance Table')

daily_dollar_volume = request.security(syminfo.tickerid, "1D", volume[1] * close[1]) //ë¦¬í˜ì¸íŒ…ì„ ë°©ì§€í•˜ê¸° ìœ„í•´ ê±°ë˜ëŒ€ê¸ˆì´ ì™„ì„±ëœ ì´ì „ ì¼ë´‰ì˜ (ê±°ë˜ëŸ‰ * ì¢…ê°€)ë¥¼ ìš”ì²­í•˜ì—¬ ì •í™•í•œ ì¼ì¼ ê±°ë˜ëŒ€ê¸ˆì„ ê°€ì ¸ì˜µë‹ˆë‹¤.
daily_avg_dollar_volume = ta.sma(daily_dollar_volume, 48) // ìµœê·¼ 48ì¼ë´‰ê°„ 1ë´‰ í‰ê·  ê±°ë˜ëŒ€ê¸ˆì„ ì‚°ì¶œí•œ ë‹¤ìŒ 86400ìœ¼ë¡œ ë‚˜ëˆ„ì–´ ì´ˆë‹¹ ê±°ë˜ëŒ€ê¸ˆì„ ê³„ì‚° í•¨
if show_stats and inDate and barstate.islastconfirmedhistory
    Invest_amount = strategy.equity/1000000
    total_trades = strategy.closedtrades
    winning_trades = strategy.wintrades
    losing_trades = strategy.losstrades
    win_rate = total_trades > 0 ? winning_trades / total_trades * 100 : 0
    profit_factor = strategy.grossloss != 0 ? strategy.grossprofit / math.abs(strategy.grossloss) : 0
    total_return = strategy.initial_capital > 0 ? (strategy.netprofit / strategy.initial_capital) * 100 : 0
    
    Invest_amount_vs_avg_bar_TradeAmount_ratio = daily_avg_dollar_volume > 0 ? (Invest_amount/(daily_avg_dollar_volume/86400)) * 100 : 0 // ë‚´ íˆ¬ìê¸ˆì•¡ì„ ì´ˆë‹¹ ê±°ë˜ëŒ€ê¸ˆ ëŒ€ë¹„ ë¹„ìœ¨ì„ ê³„ì‚°  
    string ratio_formatted = str.tostring(Invest_amount_vs_avg_bar_TradeAmount_ratio, Invest_amount_vs_avg_bar_TradeAmount_ratio >= 1 ? "#,###.##" : "#.###############") + "%"
    string ratio_header = "íˆ¬ìì´ì•¡/ì´ˆë‹¹ ê±°ë˜ëŒ€ê¸ˆ"
    var table stats_table = table.new(stats_position, 8, 2, border_width=1)
    
    table.cell(stats_table, 0, 0, "ì´ìˆ˜ìµë¥ ", text_size=size.auto, bgcolor=color.new(color.gray, bg_transp), text_color=color.new(color.white, text_transp))
    table.cell(stats_table, 1, 0, "PF", text_size=size.auto, bgcolor=color.new(color.gray, bg_transp), text_color=color.new(color.white, text_transp))
    table.cell(stats_table, 2, 0, "ì´ê±°ë˜", text_size=size.auto, bgcolor=color.new(color.gray, bg_transp), text_color=color.new(color.white, text_transp))
    table.cell(stats_table, 3, 0, "ìŠ¹ë¥ ", text_size=size.auto, bgcolor=color.new(color.gray, bg_transp), text_color=color.new(color.white, text_transp))
    table.cell(stats_table, 4, 0, "Avg Win", text_size=size.auto, bgcolor=color.new(color.gray, bg_transp), text_color=color.new(color.white, text_transp))
    table.cell(stats_table, 5, 0, "Avg Loss", text_size=size.auto, bgcolor=color.new(color.gray, bg_transp), text_color=color.new(color.white, text_transp))
    table.cell(stats_table, 6, 0, "Max DD", text_size=size.auto, bgcolor=color.new(color.gray, bg_transp), text_color=color.new(color.white, text_transp))
    table.cell(stats_table, 7, 0, ratio_header, text_size=size.auto, bgcolor=color.new(color.gray, bg_transp), text_color=color.new(color.white, text_transp), tooltip="(íˆ¬ì… ìì‚°ì´ì•¡/ì´ì „ 48ë´‰ í‰ê·  ê±°ë˜ëŒ€ê¸ˆ) * 100%")

    table.cell(stats_table, 0, 1, str.tostring(total_return, total_return >= 1000 ? "#,###" : "#") + "%", text_size=size.auto, bgcolor=total_return > 0 ? color.new(color.rgb(0, 73, 92), bg_transp) : color.new(color.red, bg_transp), text_color=color.new(color.white, text_transp))
    table.cell(stats_table, 1, 1, str.tostring(profit_factor, "#.##"), text_size=size.auto, bgcolor=profit_factor > 1 ? color.new(color.rgb(0, 73, 92), bg_transp) : color.new(color.red, bg_transp), text_color=color.new(color.white, text_transp))
    table.cell(stats_table, 2, 1, str.tostring(total_trades), text_size=size.auto, bgcolor=color.new(color.gray, 90), text_color=color.new(color.white, text_transp))
    table.cell(stats_table, 3, 1, str.tostring(win_rate, "#.#") + "%", text_size=size.auto, bgcolor=win_rate > 50 ? color.new(color.rgb(0, 73, 92), bg_transp) : color.new(color.red, bg_transp), text_color=color.new(color.white, text_transp))
    table.cell(stats_table, 4, 1, str.tostring(strategy.avg_winning_trade_percent, "#.#") + "%", text_size=size.auto, bgcolor=color.new(color.rgb(0, 73, 92), bg_transp), text_color=color.new(color.white, text_transp))
    table.cell(stats_table, 5, 1, str.tostring(strategy.avg_losing_trade_percent, "#.#") + "%", text_size=size.auto, bgcolor=color.new(color.red, bg_transp), text_color=color.new(color.white, text_transp))
    table.cell(stats_table, 6, 1, str.tostring(strategy.max_drawdown_percent, "#.#") + "%", text_size=size.auto, bgcolor=color.new(color.red, bg_transp), text_color=color.new(color.white, text_transp))
    table.cell(stats_table, 7, 1, ratio_formatted, text_size=size.auto, bgcolor=Invest_amount_vs_avg_bar_TradeAmount_ratio < Risk_level ? color.new(color.green, 60) : color.new(color.purple, 60), text_color=color.new(color.white, text_transp))